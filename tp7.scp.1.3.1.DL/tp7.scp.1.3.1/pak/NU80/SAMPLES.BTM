@echo off
cls
echo %@upper[%0]:
text

     Этот файл содержит несколько примеров командных файлов. Мы приглаша-
     ем вас изучить их совместно с документацией по NDOS. Содержимое мож-
     но распечатать содержимое с помощью PRINT или LIST и изменить любую
     часть, которую захотите. Различные сегменты отделяются друг от друга
     строкой дефисов ("-"), так что можно легко извлечь определенную
     составляющую, в которая представляет наибольший интерес.

     Здесь приводятся:

       STATUS.BTM    - Дает быстрое описание системы.
       NMENU.BTM     - Очень простая система меню.
       ONCEADAY.BTM  - Выполняет команду в течение первой за день
                       начальной загрузки.
endtext
pause

: ---------------------------------------------------------------------
rem STATUS.BTM
rem
rem  В этом примере командного файла будут использованы внутренние
rem  переменные функций NDOS для описания текущей конфигурации.
rem
cls
echo.^echo.
echo Состояние системы:
echo.
echo Дата: %_dow %_date
echo Время: %_time
echo Версия NDOS: %_nver
echo Операционная система: %_dos
echo Версия DOS: %_dosver
echo Центральный процессор: 
iff %_cpu == 86 then
  echo 8088/8086
elseiff %_cpu == 186 then
  echo 80188/80186
elseiff %_cpu == 200 then
  echo V20/V30
elseiff %_cpu == 286 then
  echo 80286
elseiff %_cpu == 386 then
  echo 80386
elseiff %_cpu == 486 then
  echo 80486
endiff
if %_ndp ne 0 echo %_ndp Обнаружен математический сопроцессор
echo Видео режим: %_video
echo Тип монитора: %_monitor
quit


: ---------------------------------------------------------------------
: NMENU.BTM
:      Этот пример командного файла создаст простое "меню" для запуска
:      наиболее часто используемых программ.
:
:      Отметим, что вместо определения REM здесь используется другой
:      способ создания "пустых" меток - закоментированные строки
:      начинающихся символом ":"
:
@echo off
:      Сначала мы определяем пару псевдонимов, которые будут использо-
:      ваться несколько раз в общей части этого командного файла, затем
:      сохраняем любой существующий псевдоним с тем же именем с помощью
:      команды SETLOCAL.
setlocal
alias in `pushd %1 ^ %2& ^ popd`
alias choice `elseiff "%userchoice" == "%1" then`
:
:       Сначала очищаем экран
cls
:dispmenu
:       Помещаем курсор в начальное положение
screen 8 0
:       Отображаем меню
text

 Выберите нужное:

      0. ВЫХОД
      1. Обработка текста
      2. Электронная таблица
      3. Связь

endtext
:      Просит пользователя ввести значение для переменной системы CHOICE
inkey Which? %%userchoice
:      Пользователь хочет выйти из меню?
iff "%userchoice" == "0" then ^ quit
:      Проверка допустимых опций
:      Каждая строка соответствует выбору меню
: Параметры - имена каталогов, к которым нужно идти, затем запускаемая
: программа.
  choice 1 ^ in c:\letters   c:\ws\ws.exe
  choice 2 ^ in d:\finance   c:\quattro\q.exe
  choice 3 ^ in c:\awremote  c:\awremote\awremote.exe
else
:      Пользователь ввел неверный параметр
  scrput 23 0 bri whi on red Неверный выбор, попробуйте еще
endiff
:      Возврат цикла в начало опроса
goto dispmenu


: ---------------------------------------------------------------------
: ONCEADAY.BTM
@echo off
:
:       Этот пакетный файл будет запускать определенную команду, когда
:       она вызывается первый раз каждый день после 6:00 утра,
:       например:
:
:               ONCEADAY NDD C: /Q
:
:      Отметим, что вместо определения REM здесь используется другая
:      практика создания "пустых" меток - закоментированные строки
:      начинаются символом ":".
:
:
: Сначала мы обнулим системную переменную LASTDATE после сохранения лю-
: бого текущего значения, которое она может уже иметь, командой SETLOCAL.
setlocal
:  lastdate=0
:  Существует ли уже файл с именем ONCEADAY.DAT
:  в корневом каталоге на загрузочном диске ?
iff exist %_boot:\onceaday.dat then
: Если да, то LASTDATE присвоить содержимое первой строки
  set lastdate=%@line[%_boot:\onceaday.dat,0]
endiff
:       Дата в файле позднее чем сегодняшняя?
:       (функция @DATE преобразует дату в целое число)
iff %@date[%_date] gt %lastdate then
:       Если да, сейчас позже 6:00 ? (произвольное время)
  iff %time[%_time] gt %@time[06:00] then
:       Да! Вызовите команду, которая передана как аргумент в этот
:       пакетный файл, затем вернитесь
    call %&
:      После выполнения команды, помещаем сегодняшнюю дату в целом
:      формате в файл ONCEADAY.DAT
    echo %@date[%_today] >! %_boot:\onceaday.dat
  endiff
endiff
:



